buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.openapitools:openapi-generator-gradle-plugin:4.1.2"
    }
}

apply plugin: 'java-library'
apply plugin: 'com.github.dcendents.android-maven'
group = 'com.github.alevadski'
apply plugin: 'org.openapi.generator'

configurations {
    generatedCompile
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'io.swagger:swagger-annotations:1.5.23'
    implementation 'org.jetbrains:annotations-java5:15.0'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
}

sourceCompatibility = "7"
targetCompatibility = "7"

def swaggerSourceFile = 'spec.yml'
def swaggerTargetFolder = 'src/main/'

task generateModels(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "java"
    inputSpec = "$projectDir/$swaggerSourceFile".toString()
    outputDir = "$projectDir".toString()
    modelPackage = "com.anchorfree.datafoundation.model"
    packageName = "com.anchorfree.datafoundation"
    configOptions = [
            dateLibrary: "java8"
    ]
    systemProperties = [
            modelDocs: "false",
            apis     : "false",
            models   : ""
    ]
    skipValidateSpec = true
    logToStderr = true
    generateAliasAsModel = false
    generateModelTests = false
    enablePostProcessFile = false
}

clean.doFirst {
    delete("${projectDir}/$swaggerTargetFolder/com/anchorfree/datafoundation/generated")
    delete("${projectDir}/docs")
}

sourceSets {
    generated {
        compileClasspath = configurations.generatedCompile
    }
    main {
        compileClasspath += generated.output
        runtimeClasspath += generated.output
    }
}

compileGeneratedJava.dependsOn generateModels
classes.dependsOn generatedClasses
compileJava.dependsOn compileGeneratedJava